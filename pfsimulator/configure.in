dnl
dnl File:        configure.in
dnl Package:     Parflow build system
dnl Copyright:   (c) 1997-2008 Lawrence Livermore National Security, LLC
dnl Revision:    $LastChangedRevision: 2228 $
dnl Modified:    $LastChangedDate: 2008-06-24 15:06:53 -0700 (Tue, 24 Jun 2008) $
dnl Description: autoconfig configuration file
dnl

AC_INIT(config/parflow_config.h.in)
AC_CONFIG_HEADER(include/parflow_config.h:config/parflow_config.h.in)

AC_CONFIG_AUX_DIR(../acmacros)

# Get system type.
AC_CANONICAL_SYSTEM

AC_ARG_WITH(CC,
   [  --with-CC=ARG           manually set C compiler to ARG])
AC_ARG_WITH(F77,
   [  --with-F77=ARG          manually set F77 to ARG])
AC_ARG_WITH(F90,
   [  --with-F90=ARG          manually set F90 to ARG])
AC_ARG_WITH(AR,
   [  --with-AR=ARG           manually set AR to ARG])
AC_ARG_WITH(clm,
   [  --with-clm              build with CLM])
AC_ARG_WITH(oas3,
   [  --with-oas3             build with OAS3])
dnl
dnl Initialize common macros to empty strings
dnl


C_FLAGS="$CFLAGS"
F77_FLAGS="$FFLAGS"
FC_FLAGS="$FCFLAGS"
LD_FLAGS="$LDFLAGS"
CPP_FLAGS="$CPPFLAGS"

INC_PATH=
LIB_PATH=
LIB_NAME=-lm

LIBS="$LIBS -lm"

LIB_SUFFIX=.a

dnl
dnl Guess the machine architecture and set the variable ARCH
dnl
 
CASC_GUESS_ARCH

dnl
dnl Set up the C compiler.  This macro must come first and be followed
dnl AC_ISC_POSIX before other compiler tests are run.  See the autoconfig
dnl manual for details.
dnl

if test -n "$with_CC"; then
   CC=$with_CC
fi

dnl
dnl Define optimize and debug switches.
dnl By default, set optimization switches and set no debug switches.
dnl
AC_ARG_ENABLE(opt,
[  --enable-opt=STRING  Set compiler optimization switches.],
[case "$enable_opt" in
   no) opt_switches="" ;;
   yes) opt_switches="-O2" ;;
   *) opt_switches="$enable_opt" ;;
esac],
opt_switches="-O2"
)

if test -n "$opt_switches" ; then
   C_FLAGS="$opt_switches $C_FLAGS"
   F77_FLAGS="$opt_switches $F77_FLAGS"
   FC_FLAGS="$opt_switches $FC_FLAGS"
fi

dnl
dnl Check if enhanced debugging flags are supported by compiler.
dnl
DEBUG_FLAGS="-g"
AX_C_CHECK_FLAG(-g3,,[
		return 0;
          ],DEBUG_FLAGS="-g3",)

AX_C_CHECK_FLAG(-ggdb,,[
		return 0;
          ],DEBUG_FLAGS="$DEBUG_FLAGS -ggdb",)

AC_ARG_ENABLE(debug,
[  --enable-debug=STRING  Set compiler debug switches.],
[case "$enable_debug" in
   no) debug_switches= ;;
   yes) debug_switches="$DEBUG_FLAGS" ;;
   *) debug_switches="$enable_debug" ;;
esac
],
debug_switches="")

if test -n "$debug_switches" ; then
   C_FLAGS="$debug_switches $C_FLAGS"
   F77_FLAGS="$debug_switches $F77_FLAGS"
   FC_FLAGS="$debug_switches $FC_FLAGS"
fi

#
# Profiling support
#

AC_ARG_ENABLE(profiling,
[  --enable-profiling=STRING  Set compiler profiling switches.],
[case "$enable_profiling" in
   no) profiling_switches="" ;;
   yes) profiling_switches="-pg" ;;
   *) profiling_switches="$enable_profiling" ;;
esac],
profiling_switches=
)

if test -n "$profiling_switches" ; then
   C_FLAGS="$profiling_switches $C_FLAGS"
   F77_FLAGS="$profiling_switches $F77_FLAGS"
   FC_FLAGS="$profiling_switches $FC_FLAGS"
fi

AC_ARG_ENABLE(timing,
	[  --enable-timing   Enable parflow module timing.],
	AC_DEFINE(PF_TIMING),
)

AC_PROG_CC
AC_PROG_CC_STDC
AC_PROG_CPP

AC_ISC_POSIX

dnl
dnl Find ar for archiving
dnl 

AC_CHECK_PROGS(AR, $with_AR ar,)
if test -z "$AR"; then
   AC_MSG_ERROR([ar archiver not found - specify with --with-m4=ARG])
fi

dnl
dnl Find install for installing 
dnl

AC_PROG_INSTALL
if test -z "$INSTALL"; then
   AC_MSG_ERROR([install not found])
fi

dnl
dnl Find mkdir for creating directories
dnl

AC_PROG_MKDIR_P
if test -z "$MKDIR_P"; then
   AC_MSG_ERROR([mkdir not found])
fi

dnl
dnl Determine Compiler being used
dnl
CASC_INFO_CXX_ID

# Some additional compiler flags

AX_C_CHECK_FLAG(-Wall,,[
		return 0;
          ],C_FLAGS="-Wall $C_FLAGS",)

AX_C_CHECK_FLAG(-Wextra,,[
		return 0;
          ],C_FLAGS="-Wextra $C_FLAGS",)

AX_C_CHECK_FLAG(-Wconversion,,[
		return 0;
          ],C_FLAGS="-Wconversion $C_FLAGS",)

AX_C_CHECK_FLAG(-Wno-write-strings,,[
		return 0;
          ],C_FLAGS="-Wno-write-strings $C_FLAGS",)

# For insure add -Zsl option for linking
case $CC in 
   *insure*)
            LIB_NAME="$LIB_NAME -Zsl"
            ;;
esac

dnl
dnl CLM Support
dnl
dnl CLM_DIR is empty when not building with CLM.  This will case the
dnl directory to be skipped in the compilation.
AC_MSG_CHECKING(if CLM is wanted)
CLM_DIR=""
CLM_LIBS=""
CLM_LIB_DEPEND=""
PARFLOW_HAVE_CLM="no"
if test "$with_clm" = "yes"; then
   AC_MSG_RESULT(yes)
   PARFLOW_HAVE_CLM="yes"
   CLM_DIR="clm"
   CLM_LIBS="-lclm"
   CLM_LIB_DEPEND="\$(PARFLOW_LIB_DIR)/libclm.a"
   AC_DEFINE(HAVE_CLM)
else
   AC_MSG_RESULT(no)
fi
AC_SUBST(CLM_DIR)
AC_SUBST(CLM_LIBS)
AC_SUBST(CLM_LIB_DEPEND)
AC_SUBST(PARFLOW_HAVE_CLM)

dnl
dnl OAS3
dnl
PARFLOW_HAVE_OAS3="no"
if test "$with_oas3" = "yes"; then
   PARFLOW_HAVE_OAS3="yes"
   AC_DEFINE(HAVE_OAS3)
fi
AC_SUBST(PARFLOW_HAVE_OAS3)

dnl
dnl Fortran 90
dnl
if test -n "$with_F90"; then
   FC=$with_F90
fi
AC_PROG_FC

dnl Not sure why AC_PROG_FC is setting "-g"
FCFLAGS=""

AC_FC_FREEFORM(,[AC_MSG_ERROR([free format in F90 compiler does not work])])

CASC_FORTRAN_LONGLINES(FC_FLAGS="$CASC_FORTRAN_LONGLINES_FCFLAGS $FC_FLAGS", FC_FLAGS="$FC_FLAGS" )

CASC_FC_ACCESS_STREAM

CASC_FC_ACCESS_SEQUENTIAL

if test "$casc_cv_fc_access_stream" = "yes"; then
   AC_SUBST(PARFLOW_FC_ACCESS,stream)
   AC_SUBST(PARFLOW_FC_FORM,unformatted)
else
   if test "$casc_cv_fc_access_sequential" = "yes"; then
      AC_SUBST(PARFLOW_FC_ACCESS,["sequential"])
      AC_SUBST(PARFLOW_FC_FORM,["binary"])
   else
   AC_MSG_ERROR([Unable to determine syntax to use for Fortran binary files])
   fi
fi

dnl Only set these if using CLM; otherwise F90 is not
dnl needed
if test "$with_clm" = "yes"; then
   AC_FC_LIBRARY_LDFLAGS
   CASC_SPLIT_LIBS_STRING(FCLIBS,FLIBS_PATH,FLIBS_NAME)
   LIB_NAME="${LIB_NAME} ${FLIBS_NAME}"
   LIB_PATH="${LIB_PATH} ${FLIBS_PATH}"

   AC_SUBST(FC_FLAGS)
   AC_SUBST(FCLIBS)
fi

dnl
dnl Set up the F77 compiler.  Look for special Fortran compilers on
dnl AIX machines.
dnl
if test -n "$with_F77"; then
      F77=$with_F77
fi
AC_PROG_F77

CASC_FORTRAN_IMPLICIT_NONE(FC_FLAGS="$CASC_FORTRAN_IMPLICIT_NONE_FCFLAGS $FC_FLAGS", FC_FLAGS="$FC_FLAGS" )
CASC_FORTRAN_WARNING(FC_FLAGS="$CASC_FORTRAN_WARNING_FCFLAGS $FC_FLAGS", FC_FLAGS="$FC_FLAGS" )

dnl
dnl Set up the Fortran libraries.
dnl

CASC_FIND_F77LIBS
LIB_NAME="$F77LIBFLAGS $LIB_NAME"

dnl
dnl Find the X11 include directory, library directory, and associated
dnl libraries.
dnl

if test "${with_x+set}" = set; then
   AC_PATH_XTRA
   if test "yes" = "$no_x"; then
      AC_MSG_NOTICE([NOTIFICATION: Parflow configured without the X11 library])
      AC_DEFINE(LACKS_X11)
   else
      AC_DEFINE(HAVE_X11)
      X_LIBS=`echo $X_LIBS | sed 's/-R\//-R \//'`
      if test -n "$x_includes"; then
         INC_PATH="-I$x_includes $INC_PATH"
      fi
      LIB_PATH="$X_LIBS $LIB_PATH"
      LIB_NAME="$X_PRE_LIBS -lX11 $X_EXTRA_LIBS $LIB_NAME"
   fi
else
   AC_MSG_NOTICE([NOTIFICATION: Parflow configured without the X11 library])
   AC_DEFINE(LACKS_X11)
fi

dnl
dnl Check if malloc.h is present
dnl
AC_CHECK_HEADER(malloc.h,AC_DEFINE(HAVE_MALLOC_H))

dnl
dnl Checking for mallinfo
dnl
AC_MSG_CHECKING(for mallinfo)
AC_TRY_COMPILE([#include <malloc.h>], void *x=mallinfo,
  AC_DEFINE(HAVE_MALLINFO, 1, Define if you have the 'mallinfo' function.)
  AC_MSG_RESULT(yes),
  AC_MSG_RESULT(no)
)

dnl dnl
dnl dnl Set up the Fortran libraries.
dnl dnl
AC_F77_LIBRARY_LDFLAGS
CASC_SPLIT_LIBS_STRING(FLIBS,FLIBS_PATH,FLIBS_NAME)
LIB_NAME="${LIB_NAME} ${FLIBS_NAME}"
LIB_PATH="${LIB_PATH} ${FLIBS_PATH}"

dnl
dnl If MPI is requested, then determine the proper path using CASC_FIND_MPI.
dnl Note:  I added CASC_FIND_MPI_ALPHA for special case with compass cluster.
dnl For more information, see code comments for macro in casc_mpi.m4.  AMW 9/00
dnl

AC_ARG_WITH(mpi,[  --without-mpi	          Compile without MPI library.])
if test "$with_mpi" = no; then
   AC_MSG_NOTICE([NOTIFICATION: Parflow configured without the MPI library])
   AC_DEFINE(LACKS_MPI)
else

   case $ARCH in
      alpha) 
         CASC_FIND_MPI_ALPHA
      ;;
      *)
         CASC_FIND_MPI
      ;;
   esac

   if test -n "$MPIINCLUDE" -o -n "$MPILIBS" -o -n "$MPILIBDIRS"; then
      with_mpi=yes
      AC_DEFINE(HAVE_MPI)
      INC_PATH="$MPIINCLUDE $INC_PATH"
      LIB_NAME="$MPILIBS $LIB_NAME"
      LIB_PATH="$MPILIBDIRS $LIB_PATH"
   fi
fi

dnl
dnl Determine AMPS porting layer to use
dnl
CASC_SUPPORT_AMPS

dnl
dnl Check if BSD gettimeofday is present
dnl
CASC_CHECK_GETTIMEOFDAY
CASC_CHECK_BIGENDIAN

dnl
dnl Cegdb support
dnl
PARFLOW_HAVE_CEGDB="no"
CASC_SUPPORT_CEGDB
if test "${cegdb_PREFIX+set}" = set; then
  LIBS="$cegdb_LIBS $LIBS"
  CASC_SPLIT_LIBS_STRING(cegdb_LIBS,cegdb_LIB_PATH,cegdb_LIB_NAME)
  LIB_PATH="$cegdb_LIB_PATH $LIB_PATH"
  LIB_NAME="$cegdb_LIB_NAME $LIB_NAME"
  INC_PATH="$cegdb_INCLUDES $INC_PATH"
  AC_DEFINE(HAVE_CEGDB,1,CEGDB library is available so use it)
  PARFLOW_HAVE_CEGDB="yes"
fi
AC_SUBST(PARFLOW_HAVE_CEGDB)

dnl
dnl Support linking with Z lib if HDF requires it
dnl
CHECK_ZLIB()
CASC_SPLIT_LIBS_STRING(zlib_LIBS,zlib_LIB_PATH,zlib_LIB_NAME)
LIB_PATH="$zlib_LIB_PATH $LIB_PATH"
LIB_NAME="$zlib_LIB_NAME $LIB_NAME"
INC_PATH="$zlib_INCLUDES $INC_PATH"


dnl
dnl Support linking with SZ lib if HDF requires it
dnl
CHECK_SZLIB()
CASC_SPLIT_LIBS_STRING(szlib_LIBS,szlib_LIB_PATH,szlib_LIB_NAME)
LIB_PATH="$szlib_LIB_PATH $LIB_PATH"
LIB_NAME="$szlib_LIB_NAME $LIB_NAME"
INC_PATH="$szlib_INCLUDES $INC_PATH"

dnl
dnl Set variables for hdf5 support
dnl
CASC_SUPPORT_HDF5
if test "${hdf5_PREFIX+set}" = set; then
  LIBS="$hdf5_LIBS $LIBS"
  CASC_SPLIT_LIBS_STRING(hdf5_LIBS,hdf5_LIB_PATH,hdf5_LIB_NAME)
  LIB_PATH="$hdf5_LIB_PATH $LIB_PATH"
  LIB_NAME="$hdf5_LIB_NAME $LIB_NAME"
  INC_PATH="$hdf5_INCLUDES $INC_PATH"
  AC_DEFINE(HAVE_HDF5,1,HDF5 library is available so use it)
fi
AC_SUBST(hdf5_LIBS)

dnl
dnl NetCDF support
dnl
PARFLOW_HAVE_NETCDF="no"
AX_LIB_NETCDF4()
if test "${with_netcdf4}" = "yes"; then
  LIB_PATH="$NETCDF4_LDFLAGS $LIB_PATH"
  LIB_NAME="$NETCDF4_LIBS $LIB_NAME"
  INC_PATH="$NETCDF4_CFLAGS $NETCDF4_CPPFLAGS $INC_PATH"
  AC_DEFINE(PARFLOW_HAVE_NETCDF,1,NetCDF library is available so use it)
  PARFLOW_HAVE_NETCDF="yes"
fi
AC_SUBST(PARFLOW_HAVE_NETCDF)

dnl
dnl Silo support
dnl
PARFLOW_HAVE_SILO="no"
CASC_SUPPORT_SILO
if test "${silo_PREFIX+set}" = set; then
  LIBS="$silo_LIBS $LIBS"
  CASC_SPLIT_LIBS_STRING(silo_LIBS,silo_LIB_PATH,silo_LIB_NAME)
  LIB_PATH="$silo_LIB_PATH $LIB_PATH"
  LIB_NAME="$silo_LIB_NAME $LIB_NAME"
  INC_PATH="$silo_INCLUDES $INC_PATH"
  AC_DEFINE(HAVE_SILO,1,SILO library is available so use it)
  PARFLOW_HAVE_SILO="yes"
fi
AC_SUBST(PARFLOW_HAVE_SILO)

dnl
dnl If Sundials is requested, then set up the Sundials libraries and include paths.
dnl

AC_ARG_WITH(sundials,
[  --with-sundials=PATH	  Use the Sundials solver package
			  and specify where it is.],
,with_sundials='no')

if test "$with_sundials" = no; then
   AC_MSG_NOTICE([NOTIFICATION: Parflow configured without the Sundials library])
   AC_DEFINE(LACKS_SUNDIALS)
else
   sundials_dir="$with_sundials"
   if test ! -d "$sundials_dir" ; then
     AC_MSG_ERROR(
       [You MUST specify an existing path when using --with-sundials=PATH])
   fi
   AC_DEFINE(HAVE_SUNDIALS)
   LIB_PATH="$LIB_PATH -L$sundials_dir/lib"
   LIB_NAME="$LIB_NAME -lsundials_cvode -lsundials_kinsol"
   INC_PATH="-I$sundials_dir/include -I$sundials_dir/include/sundials -I$sundials_dir/include/kinsol $INC_PATH"
fi
AC_SUBST(SUNDIALS_C_FILES)

dnl
dnl Set variables for hypre support.
dnl
CASC_SUPPORT_HYPRE
if test "${hypre_PREFIX+set}" = set; then
  LIBS="$hypre_LIBS $LIBS"
  CASC_SPLIT_LIBS_STRING(hypre_LIBS,hypre_LIB_PATH,hypre_LIB_NAME)
  LIB_PATH="$hypre_LIB_PATH $LIB_PATH"
  LIB_NAME="$hypre_LIB_NAME $LIB_NAME"
  INC_PATH="$hypre_INCLUDES $INC_PATH"

  # Extract Hypre version number from config file
  hypre_include_dir=`echo $hypre_INCLUDES | sed 's/-I//'`
  hypre_version=`grep HYPRE_RELEASE_VERSION $hypre_include_dir/HYPRE_config.h`

  set x $hypre_version
  hypre_version=`echo ${4} | sed s/\"//g`

  # Seperate Hypre config number into components
  oldifs="$IFS"
  IFS=.
  set x $hypre_version
  IFS="$oldifs"
  PARFLOW_HYPRE_VERSION_MAJOR=${2}
  PARFLOW_HYPRE_VERSION_MINOR=${3}
  PARFLOW_HYPRE_VERSION_PATCH=${4}

  AC_DEFINE_UNQUOTED(PARFLOW_HYPRE_VERSION_MAJOR,$PARFLOW_HYPRE_VERSION_MAJOR,[Hypre version major])
  AC_DEFINE_UNQUOTED(PARFLOW_HYPRE_VERSION_MINOR,$PARFLOW_HYPRE_VERSION_MINOR,[Hypre version minor])
  AC_DEFINE_UNQUOTED(PARFLOW_HYPRE_VERSION_PATCH,$PARFLOW_HYPRE_VERSION_PATCH,[Hypre version patch])

  AC_MSG_CHECKING(if HYPRE compile is compatible with Parflow configuration)
  if test "$hypre_MPI" = 'yes' ; then
     AC_DEFINE([HYPRE_MPI],[1],[Hypre library is configured with MPI])
     AC_MSG_RESULT(yes)
     if test "$with_mpi" = 'no' ; then
        AC_MSG_ERROR(sequential Parflow cannot call parallel HYPRE)
     fi
     AC_DEFINE(HAVE_HYPRE,1,HYPRE library is available so use it)
     PARFLOW_HAVE_HYPRE="yes"
  elif test "$hypre_MPI" = 'no'; then
     AC_MSG_RESULT(no)
     if test "$with_mpi" = 'yes' ; then
        AC_MSG_ERROR(parallel Parflow cannot call sequential HYPRE)
     fi
     AC_DEFINE(HAVE_HYPRE,1,HYPRE library is available so use it)
     PARFLOW_HAVE_HYPRE='yes'
  else
     AC_MSG_WARN(Cannot determine if hypre was compiled with MPI)
  fi
else
  AC_MSG_NOTICE([NOTIFICATION: Parflow configured without the HYPRE library])
  AC_DEFINE([LACKS_HYPRE],[1],[Hypre library is missing])
fi
AC_SUBST(PARFLOW_HAVE_HYPRE)

dnl
dnl Set variables for SAMRAI support.
dnl
PARFLOW_HAVE_SAMRAI="no"
CASC_SUPPORT_SAMRAI
if test "${samrai_PREFIX+set}" = set; then
  LIBS="$samrai_LIBS $LIBS"
  CASC_SPLIT_LIBS_STRING(samrai_LIBS,samrai_LIB_PATH,samrai_LIB_NAME)
  LIB_PATH="$samrai_LIB_PATH $LIB_PATH"
  LIB_NAME="$samrai_LIB_NAME $LIB_NAME"
  INC_PATH="$samrai_INCLUDES $INC_PATH"
  SAMRAI_DIR="$samrai_PREFIX"
  PARFLOW_HAVE_SAMRAI="yes"
  AC_DEFINE(HAVE_SAMRAI,1,SAMRAI library is available so use it)
else
  AC_MSG_WARN([Parflow configured without the SAMRAI library])
fi
AC_SUBST(PARFLOW_HAVE_SAMRAI)
AC_SUBST(SAMRAI_DIR)

dnl
dnl Set variables for SLURM support.
dnl
PARFLOW_HAVE_SLURM="no"
CASC_SUPPORT_SLURM
if test "${slurm_PREFIX+set}" = set; then
  LIBS="$slurm_LIBS $LIBS"
  CASC_SPLIT_LIBS_STRING(slurm_LIBS,slurm_LIB_PATH,slurm_LIB_NAME)
  LIB_PATH="$LIB_PATH $slurm_LIB_PATH "
  LIB_NAME="$LIB_NAME $slurm_LIB_NAME"
  INC_PATH="$slurm_INCLUDES $INC_PATH"
  SLURM_DIR="$slurm_PREFIX"
  PARFLOW_HAVE_SLURM="yes"
  AC_DEFINE(HAVE_SLURM,1,SILO library is available so use it)
else
  AC_MSG_WARN([Parflow configured without the SLURM library])
fi
AC_SUBST(PARFLOW_HAVE_SLURM)
AC_SUBST(SLURM_DIR)


debug_ext=debug
if test "$enable_opt" = "yes"; then
   if test "$enable_debug" = "yes"; then
      debug_ext=optdebug
   else
      debug_ext=opt
   fi
fi

if test "$prefix" = "NONE"; then
   dir_place_holder=`pwd`
   cd $srcdir/..
   prefix=`pwd`
   cd $dir_place_holder
fi

INSTDIR=$prefix

dnl
dnl Make all of the macro substitutions for the generated output files
dnl

AC_SUBST(ARCH)
AC_SUBST(INSTDIR)

AC_SUBST(M4)
AC_SUBST(AR)
AC_SUBST(XARGS)

AC_SUBST(F77)
AC_SUBST(F77_FLAGS)
AC_SUBST(F77LIBFLAGS)

AC_SUBST(CC)
AC_SUBST(C_FLAGS)
AC_SUBST(LD_FLAGS)

AC_SUBST(LIB_SUFFIX)

AC_SUBST(SRCDIR_COMPILE_PREFIX)
AC_SUBST(INC_PATH)
AC_SUBST(LIB_PATH)
AC_SUBST(LIB_NAME)

dnl
dnl Generate the output files.
dnl

dnl Find all Makefile.in and associated subdirectories.  
subdirs=`cd $srcdir; find . -name Makefile.in -print | sed 's%/Makefile.in$%%g' | sort `
makefiles=
for i in $subdirs; do makefiles="$makefiles $i/Makefile"; done
$MKDIR_P $subdirs
$MKDIR_P lib 
$MKDIR_P bin

AC_OUTPUT([
Makefile
config/Makefile.config 
clm/parflow_config.F90
${makefiles}
],[
dnl These are the commands that will execute after the file generations.
dnl They are run by the generated config.status script.

rm -rf Templates.DB ti_files ii_files

$MKDIR_P config include lib bin $subdirs

],
[

dnl These commands set the environment for commands executing
dnl after file generations.
srcdir=$srcdir
subdirs="$subdirs"
sundials_dir=$sundials_dir
if test ! `cd $srcdir && pwd -P` = `pwd -P`; then
  source_tree_is_not_compile_tree=1
fi
]

)
